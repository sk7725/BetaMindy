plugins{
    id "java"
}

group pGroup

sourceCompatibility = 1.8
sourceSets.main{
    java.srcDir("src/")
    resources.srcDir("assets/")
}

repositories{
    mavenCentral()
    maven{url "https://jitpack.io"}
}

dependencies{
    annotationProcessor 'com.github.Anuken:jabel:34e4c172e65b3928cd9eabe1993654ea79c409cd'
    implementation "com.github.Anuken.Mindustry:core:$pVer"
}

ext{
    sdkRoot = System.getenv("ANDROID_HOME")
    sdkVersion = '30'
}

jar.archiveFileName.set("raw-$pArtifactFilename");

def isWindows = System.getProperty("os.name").toLowerCase(Locale.ROOT).contains("windows");

task alphableed doFirst{
    exec{
        workingDir "$rootDir"

        println "Alpha-bleeding sprites..."
        if(isWindows){
            commandLine("cmd", "/c", "alpha-bleeding-windows.exe", "--replace", "./assets/sprites/")
        }else{
            commandLine("./alpha-bleed", "./assets/sprites/")
        }
    }
}

task dexify(type: Jar){
    archiveFileName.set(pArtifactFilename)

    final File jarArtifact = new File(tasks.jar.archiveFile.get().asFile.parent, "raw-$pArtifactFilename"),
               dexedArtifact = new File(tasks.dexify.getTemporaryDir(), "dexed.jar")


    doFirst{
        //collect dependencies needed for desugaring
        def files = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File("$sdkRoot/platforms/android-$sdkVersion/android.jar")])

        exec{
            workingDir dexedArtifact.parent
            def command = ["d8", "--min-api", pMinApi]
            for(def file : files){
                command += "--classpath"
                command += file.path
            }

            command += ["--output", dexedArtifact, jarArtifact]

            if(isWindows){
                commandLine("cmd", "/c", *command)
            }else{
                commandLine(*command)
            }
        }
    }

    from(zipTree(jarArtifact), zipTree(dexedArtifact))
}

task buildDex dependsOn "build", "dexify"

tasks.withType(JavaCompile){
    targetCompatibility = 8
    sourceCompatibility = JavaVersion.VERSION_14
    options.encoding = "UTF-8"
    options.forkOptions.jvmArgs.addAll([
        '--add-opens=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
        '--add-opens=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED'
    ])
    options.compilerArgs.addAll(['--release', '8'])
    compileJava.options.fork = true
}
